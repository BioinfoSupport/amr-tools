

include { MINIMAP2_ALIGN_ASM5   } from './modules/minimap2/align_asm5'
include { BCFTOOLS_ASM5_MPILEUP } from './modules/bcftools/asm5_mpileup'
include { RMD_RENDER            } from './modules/rmd/render'
include { validateParameters; paramsSummaryLog; samplesheetToList } from 'plugin/nf-schema'


process ROTATE_FASTA {
	  container "registry.gitlab.unige.ch/amr-genomics/rscript:v2"
    memory '8 GB'
    cpus 1
    time '30 min'
    input:
    		tuple(val(meta),path("query.fasta"),path("aln.bam"))
    		path(extra)
    output:
        tuple(val(meta),path('rotated_query.fasta'))
    script:
				"""
				#!/usr/bin/env Rscript
				source("assets/lib_bam.R")
				rotate_queries("query.fasta","aln.bam") |>
					writeXStringSet("rotated_query.fasta")
				"""
}


workflow ROTATE_QUERIES {
	take:
		query_ch
	main:
		query_ch | MINIMAP2_ALIGN_ASM5
		
		def rotated_query_ch = ROTATE_FASTA(
			query_ch
			.map({m,r,q -> [m,q]})
			.combine(MINIMAP2_ALIGN_ASM5.out.bam,by:0),
			file("${projectDir}/assets")
		)
		.combine(query_ch,by:0)
		.map({m,rq,r,q -> [m,r,rq]})
	emit:
		rotated_queries = rotated_query_ch
}


workflow {
	main:
		// Validate parameters and print summary of supplied ones
		validateParameters()
		log.info(paramsSummaryLog(workflow))

		def ref = file(params.ref)
		def query_ch = Channel.fromPath(params.query).map({
				def id = it.name.replaceAll(/\.(fasta|fna|fa)$/,'')
				[[sample_id:id],ref,it]
		})

		if (params.rotate_query) {
			query_ch = ROTATE_QUERIES(query_ch)
		}

		// Map (rotated) queries to REF
		query_ch
		| MINIMAP2_ALIGN_ASM5
		
		MINIMAP2_ALIGN_ASM5.out.bam
		| map({meta,bam -> [meta,ref,bam]})
		| BCFTOOLS_ASM5_MPILEUP

		RMD_RENDER(
			MINIMAP2_ALIGN_ASM5.out.bam
				.combine(BCFTOOLS_ASM5_MPILEUP.out.txt,by:0)
				.map({meta,bam,txt -> [meta,[bam,txt],"bam_file='${bam}',txt_file='${txt}'"]}),
			file("${projectDir}/assets/bam_report.qmd"),
			file("${projectDir}/assets")
		)

	publish:
		query_fa = query_ch.map({m,r,q -> [m,q]})
		bam = MINIMAP2_ALIGN_ASM5.out.bam
		bai = MINIMAP2_ALIGN_ASM5.out.bai
		mut_vcf = BCFTOOLS_ASM5_MPILEUP.out.vcf
		mut_vcf_csi = BCFTOOLS_ASM5_MPILEUP.out.vcf_csi
		mut_txt = BCFTOOLS_ASM5_MPILEUP.out.txt
		html = RMD_RENDER.out.html
}

output {
	query_fa {
		path { m,x -> x >> "${m.sample_id}.fasta"}
	}
	bam {
		path { m,x -> x >> "${m.sample_id}.bam"}
	}
	bai {
		path { m,x -> x >> "${m.sample_id}.bai"}
	}
	mut_vcf {
		path { m,x -> x >> "${m.sample_id}.vcf.gz"}
	}
	mut_vcf_csi {
		path { m,x -> x >> "${m.sample_id}.vcf.gz.csi"}
	}
	mut_txt {
		path { m,x -> x >> "${m.sample_id}.txt"}
	}	
	html {
		path {  m,x -> x >> "${m.sample_id}.html"}
	}
	
}




